plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

dependencies {
  compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.0'
  compile 'com.github.javaparser:javaparser-core:3.0.1'
  compile 'org.funktionale:funktionale-all:1.0.0-final'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.7.5'
  testCompile "com.nhaarman:mockito-kotlin:1.3.0"
}

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
  scm {
    url 'git@github.com:prt2121/kpop.git'
    connection 'scm:git@github.com:prt2121/kpop.git'
    developerConnection 'scm:git@github.com:prt2121/kpop.git'
  }
  developers {
    developer {
      id "prt2121"
      name "Prat Tanapaisankit"
      email "prt2121@users.noreply.github.com"
    }
  }
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      from components.java
      groupId 'com.github.prt2121'
      artifactId 'kpop-core'
      version '0.1.1'
      pom.withXml {
        def root = asNode()
        root.appendNode('description', 'generate Kotlin extensions from Java static methods')
        root.appendNode('name', 'kpop-core')
        root.appendNode('url', 'https://github.com/prt2121/kpop')
        root.children().last() + pomConfig
      }

      artifact sourcesJar
      artifact javadocJar
    }
  }
}

bintray {
  user = 'prt2121' //System.getenv('BINTRAY_USER')
  key = '' //System.getenv('BINTRAY_KEY') System.getenv doesn't work
  publications = ['MyPublication']
  pkg {
    repo = 'maven'
    name = 'kpop-core'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/prt2121/kpop.git'
    version {
      name = '0.1.1'
      desc = 'kpop core 0.1.1'
      released  = new Date()
      vcsTag = '0.1.1'
      gpg {
        sign = true
        passphrase = ''
      }
      mavenCentralSync {
        sync = true
        user = 'prt2121' //OSS user token
        password = '' //OSS user password
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}
